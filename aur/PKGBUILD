# Maintainer: me

pkgname=trctl
pkgver=2.7.5
pkgrel=1
arch=('any')
source=("$pkgname::git+file://$PWD?signed")
md5sums=(SKIP)
optdepends=('s-nail')
makedepends=('mold')
backup=(
"etc/$pkgname/completed.toml"
)

_release=release

pkgver() {
  cd "$pkgname"
  git describe | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$pkgname"
  export RUSTUP_TOOLCHAIN=stable
#force update for 'rustup' package users (not necesarry for 'rust' package users)
  pacman -Qo $(which cargo) | grep -q rustup && rustup update
  cargo fetch --locked --target  "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"
  mold -run cargo build --profile "$_release" --target-dir target
}

check() {
  cd "$pkgname"
  mold -run cargo test
  mold -run cargo test --doc
}

package() {
  cd "$pkgname"
  install -D -m644 configs/sysusers.conf "$pkgdir"/usr/lib/sysusers.d/"$pkgname".conf
  install -D -m644 configs/trmv.desktop "$pkgdir"/usr/share/applications/trmv.desktop
  install -D -m644 configs/trmv-magnet.desktop "$pkgdir"/usr/share/applications/trmv-magnet.desktop
  install -D -m644 configs/bittorrent.png "$pkgdir"/usr/share/pixmaps/trmv.png

  local _builddir=release
  if [ "$_release" = dev ]; then
      _builddir=debug
  fi
  install -D -m755 target/"$_builddir"/trmv "$pkgdir"/usr/bin/trmv
  install -D -m755 target/"$_builddir"/trctl "$pkgdir"/usr/bin/trctl

  install -D -m755 scripts/move.sh "$pkgdir"/usr/lib/"$pkgname"/move.sh
  install -D -m755 scripts/completed.sh "$pkgdir"/usr/lib/"$pkgname"/completed.sh
  install -D -m644 scripts/completed.toml "$pkgdir"/etc/"$pkgname"/completed.toml
  mkdir -p "$pkgdir"/usr/share/zsh/site-functions/
  #target/release/trctl gen-completions zsh > "$pkgdir"/usr/share/zsh/site-functions/_trctl
  #chmod 644 "$pkgdir"/usr/share/zsh/site-functions/_trctl
  install -D -m644 scripts/_trctl "$pkgdir"/usr/share/zsh/site-functions/_trctl
}
